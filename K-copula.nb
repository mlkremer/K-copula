(*Standard normal CDF*)
\[CapitalPhi][x_] := 1/2 + 1/2 Erf[x/Sqrt[2]];

(*Marginal CDF (Fm) and PDF (fm) of the K-distribution*)
Fm[x_, m_] := 
  1/Gamma[m/2] NIntegrate[
    z^(m/2 - 1) Exp[-z] \[CapitalPhi][x Sqrt[m/(2 z)]], {z, 0, 
     Infinity}];
fm[x_, m_] := 
  1/Gamma[m/2] NIntegrate[
    z^(m/2 - 1) Exp[-z] Exp[-m/(4 z) x^2]/Sqrt[4 \[Pi] z/m], {z, 0, 
     Infinity}];

(*Joint (bivariate) CDF (Fj) and PDF (fj) of the K-distribution*)
Fj[x_, y_, m_, c_] := 
  1/Gamma[m/2] NIntegrate[
    z^(m/2 - 1) Exp[-z] 1/Sqrt[1 - c^2]  m/(4 \[Pi] z)
      Exp[-(m/(4 z)) (\[Xi]^2 - 2 c \[Xi] \[Zeta] + \[Zeta]^2)/(
       1 - c^2)], {z, 0, \[Infinity]}, {\[Xi], -\[Infinity], 
     x}, {\[Zeta], -\[Infinity], y}, 
    PrecisionGoal -> 5]; (*PrecisionGoal -> 6*)
fj[x_, y_, m_, c_] := 
  1/Gamma[m/2] NIntegrate[
    z^(m/2 - 1) Exp[-z] 1/Sqrt[1 - c^2]  m/(4 \[Pi] z)
      Exp[-(m/(4 z)) (x^2 - 2 c x y + y^2)/(1 - c^2)], {z, 
     0, \[Infinity]}];

(*Bivariate K-copula (CDF)*)
KCopulaCDF[c_, N_, BinSize_] := Block[{b = BinSize, m = N},
  Clear@Fmi; (*necessary since Fmi works with Caching*)  
  tab = Table[{Fm[x, m], x}, {x, -4., 4., 
     0.1}]; (*sufficient for m \[Element] [0.1,500] (i.e. always)*)
  (*Inverse marginal CDF of the K-distribution*)
  Fmi[u_] := Fmi[u] = 
    Piecewise[{{-\[Infinity], u == 0}, {\[Infinity], u == 1}}, 
     Interpolation[tab][u]];
  CopulaCDF[u_, v_] := Fj[Fmi[u], Fmi[v], m, c] // Quiet;
  ParallelTable[Table[CopulaCDF[u, v], {v, 0, 1, b}], {u, 0, 1, b}]
  ]
  
(*Bivariate K-copula density (PDF)*)
KCopulaPDF[c_, N_, BinSize_] := 
 Differences[KCopulaCDF[c, N, BinSize], {1, 1}]/b^2
 
(*Auxiliary function: expands an array so that ListPlot3D correctly 
represents plots at the edges with option InterpolationOrder -> 0.*)
handicap[arr_] := {arr1 = Transpose@Append[arr, Last@arr]; 
  Transpose@Append[arr1, Last@arr1]}

(*BinSize and DataRange*)
b = 0.05;
dr = {{b/2, 1 - b/2}, {b/2, 1 - b/2}};

(*Parameters for K-copula*)
c = 0.39;
m = 7.8;

(*Calculate K-copula PDF*)
kcopPDF = KCopulaPDF[c, m, b];
maxkcopPDF = Max@kcopPDF; (*maximum value for PlotRange*)

(*Plot K-copula PDF*)
ListPlot3D[handicap@kcopPDF, PlotRange -> {0, maxkcopPDF}, 
 DataRange -> dr, 
 ColorFunction -> 
  Function[{x, y, z}, ColorData["Rainbow"][z/maxkcopPDF]],
 ColorFunctionScaling -> False, 
 AxesStyle -> Directive[Black, Thickness@0.002, 16], 
 AxesLabel -> {"\!\(\*
StyleBox[\"u\",\nFontSlant->\"Italic\"]\)", "  \!\(\*
StyleBox[\"v\",\nFontSlant->\"Italic\"]\)", ""}, 
 InterpolationOrder -> 0, Filling -> Bottom, 
 FillingStyle -> GrayLevel[1], Mesh -> None]
 
(*Calculate K-copula CDF*)
kcopCDF = KCopulaCDF[c, m, b];

(*Plot K-copula CDF*)
ListPlot3D[handicap@kcopCDF, PlotRange -> {-0.0001, 1}, 
 DataRange -> dr, 
 ColorFunction -> Function[{x, y, z}, ColorData["Rainbow"][z]],
 ColorFunctionScaling -> False, 
 AxesStyle -> Directive[Black, Thickness@0.002, 16], 
 AxesLabel -> {"\!\(\*
StyleBox[\"u\",\nFontSlant->\"Italic\"]\)", "  \!\(\*
StyleBox[\"v\",\nFontSlant->\"Italic\"]\)", ""}, 
 InterpolationOrder -> 0, Filling -> Bottom, 
 FillingStyle -> GrayLevel[1], Mesh -> None]
